= Step Aliasing

Step Aliasing allows library developers to cast the same step to one or more step names at runtime by using the `@StepAlias` annotation.

By default, steps will assume the basename of the files that define them. i.e, a `build.groovy` step file will create a `build` step. 

Step Aliasing, allows you to change the name (or names) of the step that's going to be created. 

This annotation is automatically imported, just like xref:lifecycle_hooks.adoc[lifecycle hooks].

== Static Step Aliases

Static step aliases are static lists of strings to cast the step to at runtime.

=== Single Static Alias

`@StepAlias` can take a `String` parameter to change the name of the step at runtime.

.generic.groovy
[source, groovy]
----
@StepAlias("build") <1>
void call(){
    println "running as build!"
}
----
<1> `generic.groovy` will be invokable at runtime via `build()`

=== Multiple Static Aliases

`@StepAlias` can also accept an array of Strings to alias the step to multiple names.

.generic.groovy
[source, groovy]
----
@StepAlias(["build", "unit_test"]) <1>
void call(){
    println "running as either build or unit_test!"
}
----
<1> `generic.groovy` can be used in the pipeline as either `build()` **or** `unit_test()`

== Dynamic Step Aliases

Sometimes, aliases should themselves be determined at runtime.
This can be accomplished by providing a `dynamic` parameter that should be a `Closure` that returns a string or list of strings.

For example, if a library called `alias` had a step called `generic.groovy` then an `aliases` library paramter could be created:

.pipeline configuration
[source, groovy]
----
libraries{
  alias{
    aliases = ["build", "unit_test"] <1>
  }
}
----
<1> defines a string or list of strings to alias the `generic` step to

This `aliases` parameter can then be consumed within the dynamic step alias closure:

.generic.groovy
[source, groovy]
----
@StepAlias(dynamic = { return config.aliases }) <1>
void call(){
    println "running as either build or unit_test!"
}
----
<1> `generic.groovy` can be used in the pipeline as either `build()` **or** `unit_test()`

== Keeping the Original Step

ðŸš¨ By default, when `@StepAlias` is present in a step file, a step with the original name will **not** be created.

This behavior can be overridden via the `keepOriginal` annotation paramter.

.generic.groovy
[source,groovy]
----
@StepAlias(value = "build", keepOriginal = true) <1>
void call(){
    println "running as either build() or generic()"
}
----
<1> The `keepOriginal` parameter can be used if a step with the original step name should be created

[IMPORTANT]
====
When passing multiple annotation parameters, the default static aliases parameter should be passed as `value`.
====

== Step Introspection At Runtime

Steps have access to a `stepContext` variable.

.Step Context Variable
|===
| Field | Description

| `stepContext.name`    | The name of the step being executed. May be an alias.
| `stepContext.library` | The library that contributed the step. Will be `null` for no-op template methods or default step implementations
| `stepContext.isAlias` | boolean to determine if the current step is running under an alias.

|===

=== Determining The Current Alias

.generic.groovy
[source, groovy]
----
@StepAlias(["build", "unit_test"])
void call(){
    println "Running as ${stepContext.name}" <1>
}
----
<1> Will print "Running as build" and "Running as unit_test" depending on the alias used to invoke the step.

=== Determining If The Current Step Is Aliased

When steps run via their aliased name, the `stepAlias.isAlias` variable will be true.
If the original step name is kept via `keepOriginal`, the value of `stepContext.isAlias` will be false for the original step.

== An Example Use Case

Libraries that interact with build tools such as `npm`, `maven`, or `gradle` would often benefit from Step Aliasing.

As a simple example, let's take an `npm` example.

.pipeline configuration
[source, groovy]
----
libraries{
  npm{
    phases{
        build{
            script = "package"
        }
        unit_test{
            script = "test"
        }
    }
  }
}
----

.npm_invoke.groovy
[source, groovy]
----
@StepAlias(dynamic = { return config.phases.keySet() })
void call(){
  stage("NPM: ${stepContext.name}"){
    // determine phase configuration based on step alias
    def phaseConfig = config.phases[stepContext.name]

    // ensure package.json has the phase script target
    def packageJSON = readJSON "package.json"
    if(!packageJSON.scripts.containsKey(phaseConfig.script)){
        error "package.json does not contain script ${phaseConfig.script}"
    }

    // run npm script
    sh "npm run ${phaseConfig.script}"
  }
}
----

[NOTE]
====
This example is intentionally not production ready.

Its intent is to just show how Step Aliases could be used in a real library.
====

Previously, when writing libraries such as this, common logic around tool versioning, error checking, etc would have to be either duplicated across multiple libraries.
Sometimes, a generic invoking step would be created and accept the "phase" as a method argument from other library steps.

Step Aliasing simplifies these types of setups.
